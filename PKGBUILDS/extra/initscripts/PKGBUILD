# $Id: PKGBUILD 165801 2012-08-30 16:58:57Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=initscripts
pkgver=2012.10.1
pkgrel=1
pkgdesc="System initialization/bootup scripts"
arch=('any')
url="http://www.archlinux.org"
license=('GPL2')
provides=('initscripts-systemd')
conflicts=('initscripts-systemd')
replaces=('initscripts-systemd')
backup=(etc/inittab etc/rc.conf etc/rc.local etc/rc.local.shutdown)
depends=('glibc' 'bash' 'coreutils' 'systemd-tools' 'iproute2' 'ncurses' 'findutils' 'sysvinit-tools')
optdepends=('dhcpcd: DHCP network configuration'
            'bridge-utils: Legacy network bridging support'
            'net-tools: Legacy network support'
            'wireless_tools: Legacy wireless networking'
            'sysvinit: Legacy init support')
makedepends=(asciidoc)
install=initscripts.install
source=("ftp://ftp.archlinux.org/other/initscripts/${pkgname}-${pkgver}.tar.xz"
        'alarm.patch'
        'rc.conf')
md5sums=('a4a747e73819b81f2218cf5b9bd53703'
         'e7ec1f2d469258b91bbda84284d37c4c'
         'c618e099f522f861e08a475f8960a6c2')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p1 -i "${srcdir}/alarm.patch"
  cp "${srcdir}/rc.conf" .
  [ "${CARCH}" == "arm" ]    && sed -i 's/@TTY@/ttyS0/' inittab
  [ "${CARCH}" == "armv6h" ] && sed -i 's/@TTY@/ttyS0/' inittab
  [ "${CARCH}" == "armv7h" ] && sed -i 's/@TTY@/ttyO2/' inittab

  make DESTDIR=${pkgdir} install
}
