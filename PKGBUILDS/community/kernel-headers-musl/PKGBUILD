# Maintainer: Jens Staal <staal1978@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - ensure ARCH is defined as "arm" for v6 and v7

pkgname=kernel-headers-musl
pkgver=3.12.6_5
pkgrel=1.1
pkgdesc="Linux kernel headers sanitized for use with musl libc"
arch=('i686' 'x86_64')
url="https://github.com/sabotage-linux/kernel-headers"
license=('LGPL')
depends=('musl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sabotage-linux/kernel-headers/archive/v${pkgver/_/-}.tar.gz")
md5sums=('2fe2d8053716759e42f182fbc75424e8')

build() {
  cd $srcdir/kernel-headers-${pkgver/_/-}
  _CARCH=$CARCH
  [ $CARCH == "i686" ] && _CARCH=x86
  [ $CARCH == "armv7h" -o $CARCH == "armv6h" ] && _CARCH=arm
  make ARCH=${_CARCH} prefix=/usr/lib/musl
}

package() {
  cd $srcdir/kernel-headers-${pkgver/_/-}
  _CARCH=$CARCH
  [ $CARCH == "i686" ] && _CARCH=x86
  [ $CARCH == "armv7h" -o $CARCH == "armv6h" ] && _CARCH=arm
  make ARCH=${_CARCH} prefix=/usr/lib/musl DESTDIR=$pkgdir install
}
