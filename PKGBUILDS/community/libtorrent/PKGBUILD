# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

# ALARM: jdoe0
# --enable-aligned flag is required to work correctly on armv5

pkgname=libtorrent
pkgver=0.13.6
pkgrel=3
pkgdesc='BitTorrent library with a focus on high performance and good code'
url='http://rakshasa.github.io/rtorrent/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('openssl')
#source=("http://libtorrent.rakshasa.no/downloads/${pkgname}-${pkgver}.tar.gz")
source=("$pkgname-$pkgver.tar.gz::https://github.com/rakshasa/${pkgname}/archive/${pkgver}.tar.gz"
        libtorrent-openssl-1.1.patch::"https://github.com/rakshasa/libtorrent/commit/4607bbf7.patch")
sha256sums=('bf963ac6e73e194a2cd87ebdf809988b5b3d6244bb7cd43d7d0c4852fc501524'
            '82f639c1e7cf3299c2a44a705e69286abd33a75c70d2da0594d41d5a08cd8c1a')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed '/AM_PATH_CPPUNIT/d' -i configure.ac
        # fix build against openssl 1.1
        patch -p1 -i ../libtorrent-openssl-1.1.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./autogen.sh

	export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"

	[[ $CARCH == 'arm' ]] && CONFIG='--enable-aligned'

	./configure \
		--prefix=/usr \
		--disable-debug $CONFIG

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
